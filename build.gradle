apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:4.10'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://s3.amazonaws.com/Minecraft.Download/libraries"
        url "http://repository-codingbadgers.forge.cloudbees.com/snapshot/"
    }
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}

group = 'uk.thecodingbadgers'
archivesBaseName = 'adminpack-installer'
version = '1.8.2-SNAPSHOT'
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    compile 'net.sourceforge.argo:argo:3.7'
    compile 'com.google.guava:guava:16.0.1'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'io.github.thefishlive:bootstrap:1.0.1'
    compile 'org.tukaani:xz:1.3'
    testCompile  'junit:junit:4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar, dependsOn: jar) {
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
    manifest { 
		attributes 'Main-Class': 'cpw.mods.fml.installer.SimpleInstaller'
		attributes 'Version': "${version}"
	}
}

fatJar.doFirst {
    from zipTree(jar.archivePath)
}

task shrinkJar(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    ext {
        outDir = file("${buildDir}/proguard")
        obfuscatedJar = "${outDir}/${jar.baseName}-${jar.version}.jar"
    }
    outDir.mkdirs()
    
    injars jar.archivePath
    outjars obfuscatedJar
    libraryjars configurations.compile
    configuration 'proguard.pro'
}

task shrinkOutput(type: Jar, dependsOn: shrinkJar) {
    classifier = 'shrunk'
    from zipTree(shrinkJar.obfuscatedJar)
    manifest { 
		attributes 'Main-Class': 'cpw.mods.fml.installer.SimpleInstaller'
		attributes 'Version': "${version}"
	}
}

artifacts {
    archives fatJar
    archives sourcesJar
    archives shrinkOutput
}

jar {
    classifier = 'thin'
    exclude 'argo/**'
    manifest { 
		attributes 'Main-Class': 'cpw.mods.fml.installer.SimpleInstaller'
		attributes 'Version': "${version}"
	}
}

uploadArchives {
    repositories {
        for (f in rootProject.repositories)
        {
            if (f.name.equals('filesrepo')) {
                logger.info('Publishing to files server')
                add f
        }
        }
        logger.info('Publishing to repo folder')
            add project.repositories.fileRepo
        }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
